
#逆向查询#

	=LOOKUP(1,0/(G2=B2:B6),A2:A6)

	=LOOKUP(1,0/(条件区域=指定条件),要返回的区域)

可以根据需要，将公式中的 0/(条件区域=指定条件)，写成：

	 0/((条件区域1=指定条件1)*(条件区域2=指定条件2)*……)
从而实现任意角度的多条件查询。

    http://mp.weixin.qq.com/s?__biz=MjM5NTcxODg0MA==&mid=2689794801&idx=3&sn=ccfb146aadb3392b917bddabdb8ca1bd&chksm=83539126b4241830597bf530d39416834f2c4b4d48bd475a154e86afa969f77c7fd716bb66be&mpshare=1&scene=1&srcid=0210JjQYIQ5YdjKWoPRl82ou#rd
    
	=LOOKUP(9E+307,A:A)

4^8=4的8次方=65536，也是一个比较大的数字。
因为2003版最大支持65536行，所以以前会经常看到这个数字。一般用途是排除错误值，当公式下拉超过实际返回数据行数后显示空值。
用法示例：

	=INDEX(A:A,SMALL(IF(A$2:A$5>80,ROW(A$2:A$5),4^8),ROW(A1)))&""
公式意思是将A列大于80的数值顺序输出来。
因为原始数据一共有2个大于80，当公式下拉到第3个单元格时，因为已经没有要输出的数据了，所以就引用了A列的第4^8（65536）个单元格（一个用不到的空单元格），这样就返回了空值

#1/17或5^19或5/19等等#

这几个数字有一个特点，就是运算返回的值里面包括0~9所有的10个数字。
比如：1/17=0.0588235294117647，5^19=19073486328125，5/19=0.263157894736842。
这个一般用于FIND函数在单元格中查找数字时避免出现错误值。
用法示例：

	=MIN(FIND(ROW(1:10)-1,A2&1/17))
这个公式用于查找A2单元格第一个数字出现的位置。
ROW(1:10)-1返回{0;1;2;3;4;5;6;7;8;9}，然后FIND({0;1;2;3;4;5;6;7;8;9},A2&1/17)查找0~9分别在A2&1/17中出现的位置，避免了查找某个A2中本来没有的数字出错。

比如A2单元格中不包含1，那么FIND查找1时会出错。
如果A2包含负数，可以用下面的公式来完成。
用A2&-1/17

	=MIN(FIND(LEFT(ROW($1:$11)-2),A2&-1/17))